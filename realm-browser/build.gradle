apply plugin: 'com.android.library'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion
    resourcePrefix 'realm_browser_'

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode project.gitCommitCount
        versionName project.gitTag
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 10 * 60 * 1000 // 10 minutes
        installOptions "-d", "-t"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:$project.supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$project.supportLibVersion"
    implementation "com.android.support:design:$project.supportLibVersion"
    implementation "com.android.support:support-annotations:$project.supportLibVersion"
    implementation "com.jakewharton.timber:timber:$project.timberVersion"

    testImplementation "junit:junit:4.12"
    androidTestImplementation "junit:junit:4.12"
    androidTestImplementation "com.android.support:support-annotations:$project.supportLibVersion"
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test:rules:1.0.2"
    androidTestAnnotationProcessor "io.realm:realm-annotations:$project.realmVersion"
    androidTestAnnotationProcessor "io.realm:realm-annotations-processor:$project.realmVersion"
}

apply from: rootProject.file('coverage.gradle')
apply from: rootProject.file('publish.gradle')